buildscript {
	repositories {
    	mavenCentral()
    }
    
    dependencies {
  	 	classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'  
    }
}

plugins {
    id "com.jfrog.bintray" version "1.5"
    id "eu.appsatori.fatjar" version "0.3"
}

group = 'org.bitsofinfo'

allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'java'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6


dependencies {
    
    compile group: 'com.orbitz.consul', name: 'consul-client', version:'0.17.1'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'org.glassfish.jersey.core:jersey-client:2.22.2'
    
    compile 'org.slf4j:slf4j-api:1.7.19'
    
    
	testCompile  group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    testCompile 'junit:junit:4.+'
    testCompile  group: 'log4j', name: 'log4j', version: '1.2.17'
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('bintrayUser')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('bintrayApiKey')
    publications = ['dockerDiscoveryRegistratorConsul']
    pkg {
        repo = 'maven'
        name = 'docker-discovery-registrator-consul'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bitsofinfo/docker-discovery-registrator-consul'
        publicDownloadNumbers = true
        version {
            name = project.property('version')
            desc = project.property('version') + " : " + project.property('description')
            released  = new Date()
            vcsTag = project.property('version')
        }
    }
}

publishing {
    publications {
        dockerDiscoveryRegistratorConsul(MavenPublication) {
            from components.java

            groupId project.property('group')
            artifactId 'docker-discovery-registrator-consul'
            version project.property('version')

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', project.property('group')+":docker-discovery-registrator-consul")
                asNode().appendNode('description', project.property('description'))
                asNode().appendNode('url', "https://github.com/bitsofinfo/docker-discovery-registrator-consul")
                asNode().appendNode('packaging', "jar")
                asNode().appendNode('licenses').appendNode('license')
                    .appendNode('name', "The Apache License, Version 2.0").parent()
                    .appendNode('url', "http://www.apache.org/licenses/LICENSE-2.0.txt")
                asNode().appendNode('developers').appendNode('developer')
                    .appendNode('id', "bitsofinfo").parent()
                    .appendNode('name', "bitsofinfo").parent()
                    .appendNode('email', "bitsofinfo.g@gmail.com").parent()
                    .appendNode('organization',"bitsofinfo").parent()
                    .appendNode('organizationUrl','https://github.com/bitsofinfo')
                asNode().appendNode('scm')
                    .appendNode('connection', "scm:git:https://github.com/bitsofinfo/docker-discovery-registrator-consul.git").parent()
                    .appendNode('developerConnection', "scm:git:https://github.com/bitsofinfo/docker-discovery-registrator-consul.git").parent()
                    .appendNode('url', "https://github.com/bitsofinfo/docker-discovery-registrator-consul")   
            }

        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

fatJar {
  classifier 'fat'
  manifest {
    attributes 'Main-Class': 'org.bitsofinfo.docker.discovery.registrator.consul.sample.SampleContainerApp'
  }
}



